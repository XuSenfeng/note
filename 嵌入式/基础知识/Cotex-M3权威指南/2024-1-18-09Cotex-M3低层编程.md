---
layout: post
title: "Cotex-M3低层编程" 
date:   2024-1-17 15:39:08 +0800
tags: Cotex-M3
---

# Cotex-M3低层编程

## 使用汇编

+ 无法用C写成的函数，如操作特殊功能寄存器，以及实施互斥访问。
+ 在危急关头执行处理的子程（如，NMI服务例程）。
+ 存储器极度受限，只有使用汇编才可能把程序或数据挤进去。
+ 执行频率非常高的子程，如操作系统的调度程序。
+ 与处理器体系结构相关的子程，如上下文切换。
+ 对性能要求极高的应用，如防空炮的火控系统。

## 使用C

![image-20240118125607269](https://picture-01-1316374204.cos.ap-beijing.myqcloud.com/image/202401181256301.png)

当使用C开发程序时，推荐开启CM3的双字对齐管理机制（在NVIC配置与控制寄存器中，把STKALIGN置位），代码形如：

```c
#define NVIC_CCR ((volatile unsigned long *)(0xE000ED14)) 
*NVIC_CCR ＝ *NVIC_CCR | 0x200;  //设置STKALIGN位
```

## 汇编和C接口

+ 在C代码中使用了嵌入式汇编（或者是在GNU工具下，使用了内联汇编）
+ C程序呼叫了汇编程序，这些汇编程序是在独立的汇编源文件中实现的
+ 汇编程序调用了C程序

在大多数场合下的情况都比较简单：当主调函数需要传递参数（实参）时，它们使用R0-R3。其中R0传递第一个，R1传递第2个......在返回时，把返回值写到R0中。

但若使用R4-R11，则必须在使用之前先PUSH它们，使用后POP回来。

汇编要呼叫C函数，则考虑问题的方式就有所不同：必须意识到子程序可以随心所欲地改写R0-R3, R12，却决不会改变R4 -R11。因此，如果在调用后还需要使用R0-R3,R12，则在调用之前，必须先PUSH，从C函数返回后再POP它们, 对R4-R11则不用操心。

![image-20240118131557025](https://picture-01-1316374204.cos.ap-beijing.myqcloud.com/image/202401181315070.png)

 ## 示例

```assembly
STACK_TOP EQU 0x20002000       ; SP初始值，常数
	AREA |Header Code|, CODE
	DCD         STACK_TOP           ; 栈顶（MSP的）
	DCD         Start                ; 复位向量
	ENTRY                       ; 指示程序从这里开始执行
Start                           ; 主程序开始; 初始化寄存器
	MOV         r0,         #10             ; 加载循环变量的初值
	MOV         r1,         #0          ; 初始化运算结果的值
	; 计算 10+9+8+...+1
loop    
	ADD         r1,         r0          ; R1 += R0 
	SUBS    r0,         #1          ; R0自减，并且根据结果更新标志（有”S”后缀）
	BNE         loop                ; if (R0!=0) goto loop; 
	; 现在，运算结果在R1中
deadloop
	B deadloop                  ; 工作完成后，进入无穷循环
	END                             ; 标记文件结束	
```

> ```bash
> $> armasm --cpu cortex-m3  -o test1.o test1.s
> $> armlink --rw_base 0x20000000 --ro_base 0x0 --map   -o test1.elf test1.o
> ```
>
> “--ro_base 0x0”的意思是说，把只读区（也就是程序ROM）的起始地址设为0；而“--   rw_base 0x20000000”则指定读写区（数据存储器）从0x20000000开始（在本例中，我们没有定义任何RAM数据）。“--map”选项则要求连接器给出存储器分配映射表，通过它，可以查看编译后的映像中内存的布局。
>
> ```bash
> $> fromelf --bin   --output test1.bin test1.e
> $> fromelf -c --output test1.list test1.elf # 一般不会执行这一步
> ```

## 和外界互动

在CM3中有很多调试组件，它们提供了一系列输出调试消息的方法：

+ 半主机（Semihosting）：取决于调试器与代码库的支持，可以通过NVIC的调试寄存器来做S  emihosting（通过调试探测设备，以printf的形式输出消息），第15章还要深入讨论这个主题。使用时，你要在C程序中使用printf函数，然后其输出就会显示在终端，或者显示在调试软件标准输出（STDOUT）上，具体细节还是请参阅第15章。
+ 硬件水平上支持的跟踪：如果使用的CM3单片机提供了一个跟踪接口，并且有一台外部的跟踪接口分析仪（TPA）的话，则可以解放出UART，而使用ITM来做形如printf的调试。跟踪端口就是为了这种调试而生的，它可比UART专业多了——速度快而且能提供多条信道。
+ 硬件水平上支持的跟踪——通过串行线查看器：作为后备方案，CM3的TPIU还提供了“串行线查看器（SWV）”操作模式。有了它，就可以使用远比TPA便宜的硬件来捕获从ITM发来的消息。不过，在SWV模式下，带宽并不富余，因此在需要输出大量数据时，本法就显得有些力不从心。

### 显示的示例

```assembly
UART0_BASE  EQU         0x4000C000
UART0_FLAG  EQU         UART0_BASE+0x018
UART0_DATA  EQU         UART0_BASE+0x000
Putc                                ; 该子程使用UART来发一个字符
; 入口条件： R0 = 需要发的字符
	PUSH    {R1,R2, LR}             ; 保存寄存器
	LDR         R1,     =UART0_FLAG
PutcWaitLoop
	LDR         R2,     [R1]            ; 读取状态标志
	TST         R2,         #0x20           ; 检查“发送缓冲满”标志
	BNE         PutcWaitLoop            ; 若已满则重试（若UART当掉了，则可能死循环）
	LDR         R1,     =UART0_DATA     ; 有空位时，就把UART发送站寄存器地址加载
	STRB    R0,         [R1]            ; 然后通过它把字符送给输出缓冲区
	POP         {R1,R2, PC}             ; 子程返回
```

> putc函数

```assembly
Puts                                ; 该子程往UART送一个字符串
    ; 入口条件：R0 = 待输出字符串的起始地址
    ; 这个字符串必须以零结尾（C语言格式）
    PUSH    {R0 ,R1, LR}            ; 先保存寄存器
    MOV     R1,     R0                      ; 把地址拷贝到R1，因为待会儿调用Putc时还要用    
    									; R0来传参数
PutsLoop 
	LDRB    R0, [R1],   #1          ; 读取一个字符，再自增地址
	CBZ         R0,     PutsLoopExit        ; 若已到达零字符，则执行完毕，退出
	BL      Putc                    ; 把这个字符送往UART   
	B       PutsLoop                ; 循环，以输出下一个字符
PutsLoopExit
	POP {R0, R1, PC}                ; 子程序返回
```

> puts函数

```assembly
STACK_TOP   EQU         0x20002000      ; SP初始值
UART0_BASE  EQU         0x4000C000
UART0_FLAG  EQU         UART0_BASE+0x018
UART0_DATA  EQU         UART0_BASE+0x000
	AREA | Header Code|, CODE
	DCD         STACK_TOP               ; MSP初始值
	DCD         Start                   ; 复位向量
	ENTRY
Start                               ; 主程序入口点
	MOV         r0,         #0              ; 初始化各寄存器
	MOV         r1,         #0 
	MOV         r2,         #0 
	MOV         r3,         #0 
	MOV         r4,         #0 
	BL      Uart0Initialize         ; 初始化
	UART0 LDR         r0,     =HELLO_TXT      ; 让R0指向客串的起始地址
	BL      Puts    
deadend
	B       deadend                 ; 做完了工作，在这里原地打转
	
HELLO_TXTDCB “Hello world\n”,0           ; 定义零结尾的“Hello world”END                                 ; 本文件结束	

```

> 主函数

```assembly
PutHex                              ; 以16进制输出寄存器的值
	; 入口条件：R0=要显示的值
	PUSH       {R0-R3,LR}
    MOV         R3,         R0              ; 把R0的值拷贝到R3，以便待会使用R0传递参数给
    Putc 
    MOV         R0,     #’0’              ; 先显示“0x”前缀
    BL      Putc    
    MOV         R0,     #’x’ 
    BL      Putc    
    MOV         R1,         #8              ; 初始化循环变量
    MOV         R2,         #28             ; 圆圈移位偏移量
PutHexLoop
	ROR         R3,         R2              ; 圆圈右移28格——相当于圆圈左移4格
	AND         R0,         R3,     #0xF    ; 此时最高4位移至最低4位，提取它们
	CMP         R0,         #0xA            ; 转换成ASCII码
	ITE     	GE
    ADDGE   	R0,         #55                ; 若大于等于10,则使用字母A-F表示
    ORRLT    	R0,         #0x30             ; 否则转换到0-9（原文使用ADDLT，效果相同）
    BL      Putc                    ; 输出一个hex字符
    SUBS    R1,         #1              ; 循环变量自减
    BNE         PutHexLoop              ; 检查循环变量是否已减到0，从而循环8次
    POP         {R0-R3,PC}              ; 显示完毕，子程返回
```

> 显示一个16进制数

```assembly
PutDec                              ; 以10进制输出寄存器的值
	; 入口条件：R0=要显示的值
	; 因为是32位宽，最大值（0xffff_ffff）需要10个10进制位表示，再加上零结尾，共需11字节
	PUSH    {R0-R5, LR}             ; 保存寄存器的值
	MOV         R3,         SP              ; 把当前堆栈指针拷贝到R3 
	SUB         SP,         SP,         #12         ; 为文本缓冲区保留出11个字节（因为是满栈）
	MOV         R1,         #0              ; NULL字符
	STRB    	R1,     [R3, #-1]!        ; 先把NULL字符写到字符串的结尾（把各字符逆序输出                  									; 好“负负得正”）。这里使用了更新基址的预索引
	MOV         R5,         #10            ; R5保存除数
PutDecLoop
	UDIV    R4,         R0,         R5      ; R4 = R0 / 10 
	MUL     R1,         R4,         R5      ; R1 = R4 * 10 
	SUB         R2,     R0,         R1      ; R2 = （  R0-(R0/10)*10），即个位
	ADD         R2,         0x30            ; 转换成ASCII（因为R2只能是0-9），亦可使用ORR
    STRB    R2,     [R3, #-1]!        ; 把ascii字符送进缓冲区
    MOVS    R0,         R4              ; R0 = 商，并且根更新标志位以检查商是否为零
    BNE         PutDecLoop              ; 若商为零，则已经把所有10进制位都求出
    MOV         R0,         R3              ; R0指向文本缓冲区的起始地址
    BL      Puts                    ; 使用Puts显示结果
    ADD         SP,         SP,         #12         ; 恢复SP指针
    POP         {R0-R5, PC}             ; 子程返回
```

## 使用数据存储区

在汇编源文件中定义一个相应的数据区。让连接器把数据区中的内容分派到我们指定的位置——从0x2000_0000(SRAM区的起始)

```bash
$> armlink --rw_base 0x20000000 --ro_base 0x0 --map -o test1.elf test1.o
```

```assembly
STACK_TOP EQU 0x20002000       ; SP初始值，常数
	AREA |Header Code|, CODE
	DCD         STACK_TOP           ; 栈顶（MSP的）
	DCD         Start               ; 复位向量
	ENTRY                       ; 指示程序从这里开始执行
Start                           ; 主程序开始
	; 初始化寄存器
	MOV         r0,         #10             ; 加载循环变量的初值
	MOV         r1,         #0          ; 初始化运算结果的值
	; 计算 10+9+8+...+1
loop    
    ADD         r1,         r0          ; R1 += R0 
    SUBS    r0,         #1          ; R0自减，并且根据结果更新标志（有”S”后缀）
    BNE         loop                ; if (R0!=0) goto loop;
    ; 现在，运算结果在R1中
    LDR     r0,     =MyData1
    STR     r1,     [r0]           ;把结果存入MyData1 
deadloop
    B deadloop                  ; 工作完成后，进入无穷循环
    ;定义数据区
    AREA | Header Data|, DATA
    ALIGN 4
MyData1 
	DCD         0 ; Destination of calculation result
MyData2 
	DCD         0 
	END ; 文件结束标记
```

在连接阶段，连接器要把DATA区放入读/写存储器中，因此MyData1的地址就将是我们指定的0x2000_0000。

## 使用互斥访问实现信号量

![image-20240118144925604](https://picture-01-1316374204.cos.ap-beijing.myqcloud.com/image/202401181449671.png)

## 使用位带实现互斥

![image-20240118145243797](https://picture-01-1316374204.cos.ap-beijing.myqcloud.com/image/202401181452859.png)

## 使用位段提取与查表跳转

![image-20240118145854101](https://picture-01-1316374204.cos.ap-beijing.myqcloud.com/image/202401181458154.png)

```assembly
DecodeA
	LDR         R0,=A                   ; 从内存中读取A的值
	LDR         R0,     [R0]    
	UBFX    R1, R0, #6, #2          ; R1=R0[7:6] 
	TBB         [PC, R1]
BrTable1
	DCB         ((P0 -BrTable1)/2)      ; 如果 A[7:6] = 00则跳至P0 
	DCB         ((DecodeA1-BrTable1)/2) ; 如果 A[7:6] = 01 则跳至DecodeA1，继续解码
	DCB         ((P1 -BrTable1)/2)      ; 如果 A[7:6] = 10 则跳至P1 
	DCB         ((DecodeA2-BrTable1)/2) ; 如果 A[7:6] = 10 则跳至DecodeA2 
DecodeA1
	UBFX    R1, R0, #3, #2          ; R1=R0[4:3]，准备二级解码
	TBB         [PC, R1]
BrTable2
	DCB     ((P2 -BrTable2)/2)      ; 如果 A[4:3] = 00 则跳至P2 
	DCB         ((P3 -BrTable2)/2)      ; 如果 A[4:3] = 01 则跳至P3 
	DCB         ((P4 -BrTable2)/2)      ; 如果 A[4:3] = 10 则跳至P4 
	DCB         ((P4 -BrTable2)/2)      ; 如果 A[4:3] = 11 则也跳至P4 
DecodeA2
	TST R0, #4                      ; 只需检测一个位，因此无需UBFX 
	BEQ P5 B
    P6 
P0 ...                              ; P0 
P1 ...                              ; P1 
P2 ...                              ; P2 
P3 ...                              ; P3 
P4 ...                              ; P4 
P5 ...                              ; P5 
P6 ...                              ; P6
```













































